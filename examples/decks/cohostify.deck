{deck}
version:1
card:0
size:[512,342]
name:"cohostify.deck"

{card:home}
image:"%%IMG3AgABVgQQyEmrvTjrzbv/YCiOZGmeaKqubOu+cCzPdG3feK7vfO//wKBwSCwaj8ikcslsOp/QqHRKrVqv2Kx2y+16v+CweEwum8/otHrNbrvf8Lh8Tq/b7/i8fs/v+/+AgYKDhIWGh4iJiouMjY6PkJGSk5SVlpeYmZqbnJ2en6ChoqOkpaanqKmqq6ytrq+wsbKztLW2VgG5uru8vb6/wMG/t8SBwsfIycqvDc3OzcV/ytPU1KvP2NkN0Xq6H9W+I96p2uXP3HfjHeog7N+5p+by2Oh07hr36/Ai+Z/z/87qyel3gSC+fSEMZgLI8JxANwopRCyIsF1FTw0zQnvIZqIEjxVASrzISaNGjh1JZhA5geVHlZhMykSpBqRLADdvLpLJkyYamzA35Aw6iadRn2eABhBHdGVTSEajIi2jlOlSfk8dRZU6dUxVrFcTZmW0lWvXMF/FhrW4lmrbHGWPnkU7FmfdkW/15aWr80RcuSOx3L2Vlq3Vn71w/AX8cnCRXV2Vghu6ly8vG4t74q3MJDFSyZM5OxXtJdiMzJpb9gXi2SfoyYcRm36BOnVj0kouf65LOXZSZC5qz9x8BfJu3I19G3YRurnzYSqED1ft+PHqV4XfIQ9Z3cLz7+Bb+5We0ft11udbZffQ+0T49/BR/APGsFc5891ZQMeQjON6vcqVAN+A4ZEwzzHyIOgQcSnsN5qDmx3z0H8ctOcegRg6N0KCCmqTTED4hRWOXhByN1uIEgpEoVDVpZfhi6GJwGEw5igDookpHiTMgyPyB9yEvLUo5HY6wmjkjyHUyEtDvjxj3zYonugjkjiaYJxrQRJJXYAkHOklMCYoqUtGS2az5JRURhkhmldqpyUtK+rIpVVf1vkmBTMGoNEuYurJpjq6UZRPmzgGSuJUcY42J1h2Hilfn3uO6SGhDIbIo6CcgcleeqwkOuWijGpIQ38p5BmppNgYWiWml1papKoPIpolqHI2SOoPOZbap55kltkMrJWu+ueam3oEGaeneCoorWwldqcMO7KQJ69M2tcjq9j+qSmm175KaTTK4sdss0tIGd20Te5aorDscrvube9GiWwpr4EzbmniBYfuk+VEWySaJOZbqMDePqtels3d2wXB0h5Io8MMmxdrhfFSF/HEKIn0nMJcfKsvxPyC7KcKs86lw7yKglUTyhnUh6pJJq/B8rIqr3wDefTEvLLBy6lVCc4b6ZzGzOLWTAl5QqfEs3YcIyJc0krXYOHPi0HdBtEmNp1IXFZDlF/PYFtiVtdRjzpkSeWRDQfWeGndyD9qz8H2lkbHbbd1Sxfr9t18Q/s103X3LTgPjYo6+OEnFx4j4ozjoLi9jUcu+eSUV2755ZhnrvnmnHfu+eeghy766KSXbvrpqKeu+uqst+7667DHLvvstNdu++2456777rz37vvvwAcv/PDEF2/88cgnr/zyzDfv/PPQRy/99NRXb/312Gev/fbcd+/99+CHL/745Jdv/vnop6/++uy37/778Mcv//z012///fjnr//+/Pfv//8ADKAAB0jAAhrwgAhMoAIXyMAGOvCBEIygBCdIwQpa8IIYzKAGN8jBDnrwgyAMoQhHSMISmvCEKEyhClfIwha68IUwjKEMZ0jDGtrwhjjMoQ53yMMe+vCHQAyiEIdIxCIa8YhITKISl8jEJjrxiVCMohSnSMUqWvGKWMyiFrfIxS568YtgDKMYx0jGMprxjGhMoxrXyMY2uvGNcIyjHOdIxzra8Y54zKMe98jHPvrxj4AMpCAHSchCGvKQiEykIhfJyEY68pGQjKQkJ0nJSlrykpjMpCY3yclOevKToAylKEdJylKa8pSoTKUqV8nKVrrylbCMpSxnScta2vKWuMylLnfJy1768pfADKYwh0nMYhrzmMhMpjKXycxmOvOZ0IymNKdJzWpa85rYzKY2t8nNbnrzm+AMpzjHSc5ymvOc6EynOtfJzna6MwwRAA=="
{widgets}
field1:{"type":"field","size":[174,15],"pos":[52,89],"locked":1,"border":0,"align":"center","value":"Paste Lil Code Here:"}
field2:{"type":"field","size":[174,15],"pos":[291,89],"locked":1,"border":0,"align":"center","value":"Obtain Eggbug-Compatible HTML Here:"}
code:{"type":"field","size":[234,224],"pos":[15,106],"script":"home.0","scrollbar":1,"style":"code"}
encoded:{"type":"field","size":[234,224],"pos":[263,106],"scrollbar":1,"style":"code"}

{script:home.0}
on change do
 encoded.text:highlight.html[code.text 1]
end
{end}

{module:highlight}
description:"convert lil source code into syntax-highlighted html"
version:1
{script}
inlines.pre:"background:#272822;color:#f8f8f2;padding:.5em;"
inlines.comment :"color:#75715e;"
inlines.string  :"color:#e6db74;"
inlines.escape  :"color:#ae81ff;"
inlines.keyword :"color:#f92672;"
inlines.verb    :"color:#66d9ef;"
inlines.call    :"color:#66d9ef;"
inlines.funcname:"color:#a6e22e;"

keywords:"|"split"while|each|send|on|if|elseif|else|end|do|with|local|select|extract|update|insert|into|from|where|by|orderby|asc|desc"
monads:"|"split"floor|cos|sin|tan|exp|ln|sqrt|unit|mag|heading|sum|prod|raze|max|min|count|first|last|keys|range|list|typeof|flip|rows|cols|table|ceil|trim|rev|distinct"
dyads:"|"split"split|fuse|dict|take|drop|limit|in|unless|join|cross|parse|format|like|window|fill"
verbs:monads,dyads
escapes:"\\n","\\\"","\\\\"

# tokenize the input file into an rtext-compatible table
# this gives us several useful output options,
# and also makes it easy to coalesce adjacent runs together:

on chunk type x do
 insert text font arg with x type "" end
end

on highlight text do
 r:rtext.cat[] i:fn:0
 while i<count text
  head:i drop text
  r:r,if last comm:"#%-r\n%n" parse head # line comments
   i:i+last comm
   chunk["comment" "#%s" format first comm]
  elseif last space:"%.2r\n %n" parse head # whitespace
   i:i+last space
   chunk["" first space]
  elseif "\""~first head # multiline string literals, including backslash escapes
   i:i+1
   t:chunk["string" "\""]
   s:1 while s
    head:i drop text
    if "\""~first head
     s:0 # reached terminal "
     i:i+1
     t:t,chunk["string" "\""]
    elseif (2 take head) in escapes
     i:i+2
     t:t,chunk["escape" 2 take head]
    else
     str:"%-.2r\"\\" parse head
     if !count str s:0 end # reached eof while processing a string
     i:i+count str
     t:t,chunk["string" str]
    end
   end
  elseif last tok:"%v%n" parse head # identifiers
   i:i+last tok tok:first tok
   if     tok~"on"                fn:1 chunk["keyword"  tok]
   elseif fn                      fn:0 chunk["funcname" tok]
   elseif tok in keywords              chunk["keyword"  tok]
   elseif tok in verbs                 chunk["verb"     tok]
   elseif "%*v%*.2r\n [%m" parse head  chunk["call"     tok]
   else                                chunk[""         tok]
   end
  else # misc special characters/operators
   i:i+1
   chunk["" first head]
  end
 end
 rtext.cat[r]
end

# convert rtext form into xml for output,
# taking advantage of writexml[] to manage
# all the hideous entity escaping:

html_escapes:raze insert k v with
 "\n" "<br>"
 " "  "&nbsp;"
 "\"" "&quot;"
 "'"  "&apos;"
 "<"  "&lt;"
 ">"  "&gt;"
 "&"  "&amp;"
end
html_specials:"" fuse keys html_escapes

on highlight_to_html text raw do
 r.tag:"pre"
 if raw r.attr.style:inlines.pre else r.attr.class:"lil" end
 r.children:each row in rows h:highlight[text]
  if row.font
   s.tag:"span"
   if raw s.attr.style:inlines[row.font] else s.attr.class:row.font end
   s.children:list row.text
  else
   t:row.text
   esc:array[]
   while count t
    if (first t) in html_specials  esc.cat[html_escapes[first t]] t:1 drop t
    else c:"%-.7r\n \"'<>&%n" parse t esc.cat[first c ] t:(last c) drop t
    end
   end
   list esc
  end
 end
 writexml[r]
end

module.html:highlight_to_html
{end}

