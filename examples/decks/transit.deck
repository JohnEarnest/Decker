{deck}
version:1
card:0
size:[512,342]
name:"Transition Editor"

{card:home}
script:"home.0"
{widgets}
a:{"type":"canvas","size":[102,84],"pos":[25,43],"locked":1,"image":"%%IMG3AGYAVAQQyUmrvTjrzbv/YCiOZGmeaKqubOu+cCzPdG3feK7vfO//wKBwSCwaj7RAALlTKpk5pxNqk1qv2CV1lO1ityKveAr+jM9l81mc7qzHbc6bHdfMvXX7PZvH7Lt9Fn+AgRSDhIUIh3yJi4yFjl+QkVeNlFaTl5h9b4pzgWsSd5yhnp15p6alcauqaKyaj2mxiGC0tVu3srm6lbO9vrzAm8LDZFTGklDJwUzMxEjP0EfS00XVUs5/fp/R2xej1N/g4USHGXtGg3rlQefo6UPv3O0/i+z1PfP0+U375PF8ONrwT8dAeOvsJUS4UN84fg8ddmPYMNEEShYFHcyoMSBHgHg+gmgmsqTJkyhTqlzJsqXLIhEA","scale":1}
b:{"type":"canvas","size":[102,84],"pos":[151,43],"locked":1,"image":"%%IMG3AGYAVAQwyUmrvTjrzbv/YCiOZGmeaKqubOu+cCzPdG3feK7vfO//KoBwSCwaj0DTcclsApIip3T6hHqo2Ka1k+0atxuveAjOjM/ly3mctqzF7crbG6dk3fS6BGu+6xNUYXx/gRp+dYdqg4iLioV6jXiPjFKCk3GXklN/gJkTXZxziZSilaGlpqeoS5x2q0yte69aqrOshLa0pLlIu7xfmL+wwcK9bcXDaci3ypFyeceblqNbnprWSc7X2D/cn9Bg3rKgzdKG4NXU3+hWy8zh7sBl8fLw9GTl91Xz+kLR/f/0BaSXT6A9gPwQFiwWi4SuhhAjSpxIsaLFixgzatzIsaPHjyBDihxJsiSQCA==","scale":1}
preview:{"type":"canvas","size":[102,84],"pos":[281,42],"locked":1,"image":"%%IMG3AGYAVAQQyUmrvTjrzbv/YCiOZGmeaKqubOu+cCzPdG3feK7vfO//wKBwSCwaj7RAALlTKpk5pxNqk1qv2CV1lO1ityKveAr+jM9l81mc7qzHbc6bHdfMvXX7PZvH7Lt9Fn+AgRSDhIUIh3yJi4yFjl+QkVeNlFaTl5h9b4pzgWsSd5yhnp15p6alcauqaKyaj2lvAIdtsXizuLJbu7xUvr9IwcJHxJLAx8hMysvDzZvM0NHP01JQ1tRFg36f1aMX4Ebc3d5EtuWpQ+Tp6kDo7a3v7OF76/D19kH4+eI+ixvo8XAUUKAOghkA/uAnSOG/PwUZNvHXz2AiVxYtObzYUB/HhHA+hqgksqTJkyhTqlzJsqXLPBEA","scale":1}
field1:{"type":"field","size":[77,12],"pos":[37,28],"locked":1,"show":"transparent","border":0,"style":"plain","align":"center","value":"before"}
field2:{"type":"field","size":[77,12],"pos":[163,28],"locked":1,"show":"transparent","border":0,"style":"plain","align":"center","value":"after"}
field3:{"type":"field","size":[77,16],"pos":[292,27],"locked":1,"show":"transparent","border":0,"style":"plain","align":"center","value":"preview"}
code:{"type":"field","size":[475,184],"pos":[17,142],"script":"home.1","scrollbar":1,"style":"code","scroll":304,"value":"on star c t do\n o:c.size/2\n r:(2.5*t)*o\n i:unit (pi/2)+(range 10)*(pi/5)\n p:each v index in i\n  o+(if 2%index 1 else 0.5 end*r)*v\n end\n c.poly[p]\nend\n\non StarIn c a b t do\n star[c t]\n c.merge[a b]\nend\n\non StarOut c a b t do\n star[c 1-t]\n c.merge[b a]\nend\n\non oval pos size do\n flip pos+size*flip unit 2*pi*(range 20)/20\nend\n\non heart c t do\n o:(0.5,0.6)*c.size\n r:t*c.size\n p:each v in (1,0,-1,0)join(0,-1,0,1) o+r*v end\n c.poly[p]\n c.poly[oval[o+r*( 1,-1)/2 r/sqrt 2]]\n c.poly[oval[o+r*(-1,-1)/2 r/sqrt 2]]\nend\n\non HeartIn c a b t do\n heart[c t]\n c.merge[a b]\nend\n\non HeartOut c a b t do\n heart[c 1-t]\n c.merge[b a]\nend\n\nStarIn"}
button1:{"type":"button","size":[89,31],"pos":[404,30],"script":"home.2","text":"Play"}
tweenval:{"type":"slider","size":[90,24],"pos":[403,75],"script":"home.3","interval":[0,1],"step":0.1}

{script:home.0}
on func do
 env:()
 env.pi:pi
 eval[code.text env].value
end

on frame f t do
 preview.clear[]
 f[preview a.copy[] b.copy[] t] 
end

on play do
 f:func[]
 each t in (range 31)/30
  frame[f t]
  sleep[1]
 end
end
{end}

{script:home.1}
on run do
 frame[func[] tweenval.value]
end
{end}

{script:home.2}
on click do
 play[]
end
{end}

{script:home.3}
on change val do
 frame[func[] val]
end
{end}

