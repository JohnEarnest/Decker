
# test suite for nils

print["identity:"]
show["type" typeof nil]
show["falsiness" if nil "truthy" else "falsey" end]
show["unlessiness" ("replace" unless "") ("replace" unless 0) ("replace" unless nil)]
show["match self" nil ~ nil]
show["spread (right)" nil = 11,nil,22,nil,33]
show["spread (left)" (44,55,nil,66,nil) = nil]
show["nil vs nothing" nil = nil,zilch,0,"",list ()]
show["nil compare" (nil < 0) (nil > 0) (nil < 1) (nil > 1)]
show["uninitialized var is" uninitialized]
show["default body of func" (on f do end)[]]
show["default body of each" each in 11,22,33 end]
show["default body of while" while 0 end]
show["default body of eval" eval[""].value]
show["filter in list"  nil take 11,nil,22,nil,33]
show["filter out list" nil drop 11,nil,22,nil,33]
show["filter in dict"  nil take ("A",nil,"B","C") dict (11,22,nil,44)]
show["filter out dict" nil drop ("A",nil,"B","C") dict (11,22,nil,44)]
show["filter in multi dict"  (nil,"A") take ("A",nil,"B","C") dict (11,22,nil,44)]
show["filter out multi dict" (nil,"A") drop ("A",nil,"B","C") dict (11,22,nil,44)]

print["\ncoercion:"]
show["to num" 0+nil]
show["to str" "fuse" format "[",nil,"]"]
show["to tab" select from nil]

print["\noutdexing:"]
show["firsts" (first nil) (first ()) (first "")]
show["lasts"  (last  nil) (last  ()) (last  "")]
show["outdex str" "A"[0] "A"[-1] "A"[1] "A"[5]]
show["outdex list" (11,22)[0] (11,22)[-1] (11,22)[2] (11,22)[5]]
show["outdex dict" ("ab"dict 11,22)["a"] ("ab"dict 11,22)["c"] ("ab"dict 11,22)[nil] ("ab"dict 11,22)[0]]
show["outdex tab" (insert a b with 11 22 end)["c"] (insert a b with 11 22 end)[-1] (insert a b with 11 22 end)[5]]
img:image[3,4]
show["outdex image" img[0,0] img[-1,0] img[1,-1] img[3,4]]
snd:sound[3]
show["outdex sound" snd[0] snd[-1] snd[3] snd[5] snd[-2,3] snd[2,4]]
show["nindex str" ("ABC"          )[nil]]
show["nindex lst" (11,22,33       )[nil]]
show["nindex dic" ("AB" dict 11,22)[nil]]
show["nindex set str" ("ABC"          )[nil]:55]
show["nindex set lst" (11,22,33       )[nil]:55]
show["nindex set dic" ("AB" dict 11,22)[nil]:55]

print["\npadding:"]
show["amend column" (insert a b with 11 22 33 44 end).c:list 55]
show["flip list pad" flip (list 11,22),(list 44,55,66)]
show["dict pad" "ABC" dict 11,22]
show["take pad" (3 take 0) (3 take ()) (3 take "")]

print["\nparseformat:"]
show["parse null" "%j%i" parse "null23"]
show["format null" "%j%i" format nil,23]
show["parse invalid" ("%i" parse "ape")]
show["parse missing csv col" readcsv["one,two,three\nA,11,98.6\nB,22,\nC,33," "sif"]]

print["\nfill:"]
show["fill empties" (99 fill ()) (99 fill "") (99 fill () dict ()) (99 fill table nil)]
show["fill scalars" (33 fill nil) (34 fill 0) (35 fill "a")]
show["fill lists"   (36 fill 12,nil,13,nil) (37 fill "%J" parse "[[11,null,33],[44,55],null,[66,null]]")]
show["fill dicts"   (38 fill "ABC" dict 11,22)]
show["fill tabs"    (39 fill insert a b with 11 22 33 nil nil nil end)]
